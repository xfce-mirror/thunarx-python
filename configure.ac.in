dnl Copyright (c) 2006-2020
dnl         The Thunar development team. All rights reserved.
dnl
dnl Originally written for Thunar by Benedikt Meurer <benny@xfce.org>.
dnl Modified by Peter de Ridder <peter@xfce.org>.
dnl Modified by Adam Plumb <adamplumb@gmail.com>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([txp_version_major], [0])
m4_define([txp_version_minor], [5])
m4_define([txp_version_micro], [2])
m4_define([txp_version_tag], [git])
m4_define([txp_version_build], [@REVISION@])
m4_define([txp_version], [txp_version_major().txp_version_minor().txp_version_micro()ifelse(txp_version_tag(), [], [], [txp_version_tag()-txp_version_build()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2017 Adam Plumb <adamplumb@gmail.com>.])
AC_INIT([thunarx-python], [txp_version], [https://gitlab.xfce.org/bindings/thunarx-python])
AC_PREREQ([2.69])
AC_CANONICAL_TARGET()
AC_CONFIG_MACRO_DIRS(m4)
AC_CONFIG_HEADERS([config.h])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 dist-bzip2 no-dist-gzip tar-ustar])
AM_MAINTAINER_MODE()
AM_SILENT_RULES([yes])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
LT_PATH_LD([])
AC_PROG_INSTALL()
AM_PROG_CC_C_O()

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.4])
LT_INIT([disable-static])

dnl Check for gtk-doc
GTK_DOC_CHECK(1.14)

PYG_CHECK_PYTHON_HEADERS(,[AC_MSG_ERROR(could not find Python headers)])
PYG_CHECK_PYTHON_LIBS(,[AC_MSG_ERROR(could not find Python lib)])

dnl ***************************
dnl *** Determine the installed thunarx version ***
dnl ***************************
THUNARX_API_VERSION=3
XDT_CHECK_PACKAGE([THUNARX], [thunarx-3], [1.7.0])

XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.20.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.30.0])
XDT_CHECK_PACKAGE([PYGOBJECT], [pygobject-3.0], [3.20.0])
AC_SUBST(THUNARX_API_VERSION)

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG([txp_debug_default])

dnl **************************************
dnl *** Check for linker optimizations ***
dnl **************************************
XDT_FEATURE_LINKER_OPTS()

AC_CONFIG_FILES([
Makefile
src/Makefile
examples/Makefile
docs/Makefile
docs/reference/Makefile
docs/reference/version.xml
])
AC_OUTPUT

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo

echo "      Prefix: $prefix"
echo "      Documentation: ${enable_gtk_doc}"
echo "      Python Version: ${PYTHON_LIB_NAME}"
echo
