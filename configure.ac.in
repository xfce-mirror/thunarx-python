dnl Copyright (c) 2006-2020
dnl         The Thunar development team. All rights reserved.
dnl
dnl Originally written for Thunar by Benedikt Meurer <benny@xfce.org>.
dnl Modified by Peter de Ridder <peter@xfce.org>.
dnl Modified by Adam Plumb <adamplumb@gmail.com>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([txp_version_major], [0])
m4_define([txp_version_minor], [5])
m4_define([txp_version_micro], [1])
m4_define([txp_version_tag], [git])
m4_define([txp_version_build], [@REVISION@])
m4_define([txp_version], [txp_version_major().txp_version_minor().txp_version_micro()ifelse(txp_version_tag(), [], [], [txp_version_tag()-txp_version_build()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2017 Adam Plumb <adamplumb@gmail.com>.])
AC_INIT([thunarx-python], [txp_version], [https://gitlab.xfce.org/bindings/thunarx-python])
AC_PREREQ([2.50])
AC_CANONICAL_TARGET()
AC_CONFIG_MACRO_DIR(m4)

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 dist-bzip2 no-dist-gzip tar-ustar])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()
AM_SILENT_RULES([yes])

dnl *******************************
dnl *** Check for UNIX variants ***
dnl *******************************
AC_AIX()
AC_ISC_POSIX()
AC_MINIX()

dnl ********************************
dnl *** Check for common headers ***
dnl ********************************
AC_CHECK_HEADERS([sys/wait.h])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AC_PROG_LD()
AC_PROG_INSTALL()
AM_PROG_CC_C_O()

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.4])
LT_INIT([disable-static])

dnl Check for gtk-doc
GTK_DOC_CHECK(1.14)

PYG_CHECK_PYTHON_HEADERS(,[AC_MSG_ERROR(could not find Python headers)])
PYG_CHECK_PYTHON_LIBS(,[AC_MSG_ERROR(could not find Python lib)])

dnl ***************************
dnl *** Determine the installed thunarx version ***
dnl ***************************
if test "`pkg-config --variable prefix thunarx-3`" != ""; then
    THUNARX_API_VERSION=3
    prefix=`pkg-config --variable prefix thunarx-3`
    XDT_CHECK_PACKAGE([THUNARX], [thunarx-3], [1.6.0])

    XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.20.0])
    XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.30.0])
    XDT_CHECK_PACKAGE([PYGOBJECT], [pygobject-3.0], [3.20.0])
else
    echo "thunarx-3 is required";
    exit 1;
fi
AC_SUBST(THUNARX_API_VERSION)

THUNARX_LIBDIR=`pkg-config --variable=libdir thunarx-${THUNARX_API_VERSION}`
AC_SUBST(THUNARX_LIBDIR)
AC_DEFINE_UNQUOTED(THUNARX_LIBDIR, "$THUNARX_LIBDIR", [Thunarx libdir])


dnl ---------------------------------
dnl Get thunar extensions directory
dnl ---------------------------------
THUNARX_EXTENSION_DIR=`$PKG_CONFIG --variable=extensionsdir thunarx-${THUNARX_API_VERSION}`
AC_SUBST(THUNARX_EXTENSION_DIR)
AC_DEFINE_UNQUOTED(THUNARX_EXTENSION_DIR, "$THUNARX_EXTENSION_DIR", [Thunar's extensions directory])

dnl ---------------------------------
dnl Check for gtk-doc dependencies
dnl ---------------------------------
if test "${enable_gtk_doc}" != no; then
    dnl Check for gtkdoc-mkhtml
    AC_PATH_PROG([GTKDOC_MKHTML], [gtkdoc-mkhtml])
    if test -z "$GTKDOC_MKHTML"; then
        echo " disabling generation of docs"
        enable_gtk_doc=no
    fi
fi

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG([txp_debug_default])

dnl **************************************
dnl *** Check for linker optimizations ***
dnl **************************************
XDT_FEATURE_LINKER_OPTS()

AC_OUTPUT([
Makefile
src/Makefile
examples/Makefile
docs/Makefile
docs/reference/entities.docbook
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo

echo "      Prefix: $prefix"
echo "      Documentation: ${enable_gtk_doc}"
echo "      Python Version: ${PYTHON_LIB_NAME}"
echo
